#!/usr/bin/env perl
# @author Robin Schneider <ypid23@aol.de>
# @licence GPLv3+ <http://www.gnu.org/licenses/gpl.html>
use strict;
use warnings;
use autodie;
use feature qw(say);
use utf8;
use open qw(:std :utf8);
binmode STDOUT, ':encoding(UTF-8)';

unless (@ARGV) {
    say "Please give me a file with the relevant copy of the map features wiki source or pipe it into this script.";
    exit 1;
}

while (<>) {
    if (/\|                       # first match pipe
        \s*?(?:{{{)?              # match space and optinally {{{
        ([^:{]+):desc(?:=|\|)\s*? # match key
        ([^']*)                   # match text that comes before a emphasized text
        .*?'''([^,']+)[^']*?'''   # match emphasized text, stuff after a comma in the text will not be included
        /xms and not /do not use this tag/ims) {
        my($key, $start_desc, $desc) = ($1, $2, $3);
        # say "--$start_desc--";
        next if $start_desc =~ /(?:siehe|see)/ixms;
        say "$key,$desc";
    }
}
